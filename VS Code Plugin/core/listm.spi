// Creates a list with a single element.
inl singleton x = Cons: x, Nil
// Applies a function to each element of the list, threading an accumulator argument through the computation. If the input function is f and the elements are i0...iN then computes f (... (f s i0)...) iN
inl fold f s l =
    let rec loop s = function Cons: x,x' => loop (f s x) x' | Nil => s
    loop s l
// Folds the list from the end to the beginning.
inl foldBack f l s =
    let rec loop s = function Cons: x,x' => f x (loop s x') | Nil => s
    loop s l
// Reverses a list.
inl rev l = fold (fun s x => Cons: x,s) Nil l
// Appends two lists.
inl append a b = foldBack (::) a b
// Concatenates a list of lists.
inl concat l = foldBack append l Nil
// Maps a list.
inl map f l = foldBack (fun x s => f x :: s) l Nil
// Iterates over the list.
inl iter f l = fold (fun () x => f x) () l

