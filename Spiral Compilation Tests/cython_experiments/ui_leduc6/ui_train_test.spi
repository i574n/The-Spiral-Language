inl play p1 : () =
    !!!!Import("ui_leduc")
    inl root : obj = $"ui_leduc.Top()"
    let human_dispatch (((p1,p2,card : leduc.game_state),observations),(dist : a u64 _),(next : _ -> ())) : () =
        {
        trace = agent.human.show_trace p1.id observations
        actions =
            inl actions = mut {fold= $"False"; call= $"False"; raise= $"False"}
            dist |> am.iter fun x =>
                open leduc
                match x with
                | Fold => actions.fold <- fun () => next x
                | Call => actions.call <- fun () => next x
                | Raise => actions.raise <- fun () => next x

            record *actions
        table_data =
            inl show_card = 
                open leduc
                function King => 'K' | Queen => 'Q' | Jack => 'J'
            record {
                my_card = show_card p1.card 
                my_pot = p1.pot 
                op_card = show_card p2.card 
                op_pot = p2.pot 
                community_card = match card with Some: c => show_card c  | None => ' '
                }
        } |> record |> fun r => $"!root.data = !r"
    inl p2 = agent.human.funs human_dispatch
    inl f (): () =
        open nodes
        leduc.game (nodes.cps.nodes_2p (p1, p2)) ((log_probm.from 1,init,init),fun r => ())
    $"ui_leduc.popup_game(!root,!f)"

inl main () =
    inl d : dict.t _ _ = dict.empty
    // inl uniform = agent.uniform.funsEnum()
    let run a,b = leduc.game (nodes.nodes_2p (a, b)) (log_probm.from 1,nodes.init,nodes.init)
    inl train () =
        inl play_avg, play_cur, train_cur = 
            open agent.cfr
            funsTrain PlayAvgPolicy d, funsTrain PlayCurrentPolicy d, funsTrain TrainCurrentPolicy d
        inl _ = run (train_cur,play_cur)
        inl _ = run (play_cur,train_cur)
        run (play_avg, play_avg)
    inl play () = play (agent.cfr.funsPlay d)
    
    inl show_buffer () : ra u64 _ = (dict.toArray d : a _ _) |> am.generic.map fun k,v =>
        open leduc
        open nodes
        inl show_card = function
            | King => "[color=ff0000]K[/color]"
            | Queen => "[color=ff0000]Q[/color]"
            | Jack => "[color=ff0000]J[/color]"
        inl show_action = function
            | Fold => "F"
            | Call => "C"
            | Raise => "R"
        inl pstr x = $"'{:.5f}'.format(!x)" : string
        record {
        trace =
            inl trace : ra u64 _ = am.empty
            listm.iterBack ((function Observation: x => show_card x | Action: x => show_action x) >> rm.add trace) k
            rm.join' "" trace
        avg_policy =
            am.generic.map2 (fun k v => 
                inl k,v = show_action k, pstr v in $"f'{!k}: {!v}'" : string
                ) v.actions v.avg_policy
            |> rm.join' "\n"
        regret =
            am.generic.map2 (fun k v => 
                inl k,v = show_action k, pstr v in $"f'{!k}: {!v}'" : string
                ) v.actions v.regret
            |> rm.join' "\n"
        }
    
    !!!!Import("ui_train")
    $"ui_train.run(!train,!show_buffer,!play)"
    ()