open hu_holdem

union oaction = OStreetOver | OFold | OCall | ORaiseTo: st
inl holdem_obs_to_array (l : list (observation card action)) : a st oaction =
    l |> listm.fold (fun (s,is_round_over) => function
        | Observation: x => s, true
        | Action: x => 
            inl x = match x with Fold => OFold | Call => OCall | RaiseTo: x => ORaiseTo: x
            if is_round_over then x :: OStreetOver :: s, false
            else x :: s, false
        ) ([],false) 
    |> fst |> listm.toArray

inl suit_rank x = hand_scorer.suit x, hand_scorer.rank x
inl full (suit,rank) = hand_scorer.full (suit:rank:)

inl holdem_extractor' ((player_state : pl2 card action),(p1,p2,community_card,stack,is_showdown : player * player * a st _ * st * bool),pid) = 
    ((stack, p1.pot, p2.pot, p1.hand, community_card), pl2_observations player_state pid), p2.hand

inl holdem_extractor x = inl (a,b),c = holdem_extractor' x in (a,holdem_obs_to_array b),c

inl holdem_schema () =
    inl stack_size = 10 + 1 // Don't forget the extra 1 at the end.
    inl field_size = 6 // Can represent 2 ** field_size - 1 binary values in the serializer.
    inl trace_length = stack_size + 8 // RaiseTo (3..stack_size) - RaiseTo (0,1,2) + 3 OStreetOver + 4 Check + 4 Call.
    inl policy,value =
        open serialization.dense.array
        inl stack : pu st = bin_int field_size
        inl card = wrap (in:suit_rank out:full) (int 4 ** int 13)
        inl hand = card ** card
        inl policy =
            inl oaction = alt {oStreetOver=Unit; oFold=Unit; oCall=Unit; oRaiseTo_=stack} : pu oaction
            (stack ** stack ** stack ** hand ** array 5 card) ** array trace_length oaction
        inl value = policy ** hand
        policy,value
    inl action =
        open serialization.sparse.int
        alt {fold=Unit; call=Unit; 
            raiseTo_ = wrap (in:(+) -3 out:(+) 3) (int (stack_size-3)) // RaiseTo (3..stack_size) - RaiseTo (0,1,2)
            } : pu action
    schema {policy value action}

inl holdem_cython_schema () =
    inl schema = holdem_schema()
    inl handler = train.neural_handler holdem_extractor schema
    inl size =
        inl policy = serialization.dense.array.size schema.policy
        inl value = serialization.dense.array.size schema.value
        inl action = serialization.sparse.int.size schema.action
        namedtuple "Size" {action policy value}
    namedtuple "Neural" {handler size}